/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation Deps.kotlin_stdlib

    implementation Deps.androidx_annotation
    implementation Deps.androidx_lifecycle_extensions
    implementation Deps.androidx_lifecycle_viewmodel_ss
    implementation Deps.mozilla_support_base

    implementation Deps.rxAndroid
    implementation Deps.rxKotlin

    implementation Deps.autodispose
    implementation Deps.autodispose_android
    implementation Deps.autodispose_android_aac
}

android.libraryVariants.all { variant ->
    // At this point, the Android-Gradle plugin has created all the Android tasks and
    // configurations.  This is the time for us to declare additional Glean files to package into
    // AAR files.  This handles "module" Gradle dependenices but not "project" Gradle dependencies.
    def stems = ['metrics', 'pings']
    if (stems.any { stem -> file("$projectDir/${stem}.yaml").exists() }) {
        // This packs the given file in the root of the AAR, sibling to `AndroidManifest.xml` and
        // `classes.jar`.
        stems.each { stem ->
            def f = file("$projectDir/${stem}.yaml")
            if (f.exists()) {
                variant.packageLibrary.from(f)
            }
        }
    }
}

android.libraryVariants.all { variant ->
    // At this point, the Android-Gradle plugin has created all the Android tasks and
    // configurations.  This is the time for us to declare additional Glean-specific variant
    // configurations and artifacts.  This handles "project" Gradle dependencies.
    def stems = ['metrics', 'pings']

    // This is not as stable as referring to `variant.compileConfiguration` directly: it's
    // possible the Android-Gradle plugin will change the name of this configuration.  However
    // this is unlikely: "apiElements" is listed in Table 29 as "[a] configuration[s] used by
    // consumers" at
    // https://docs.gradle.org/5.5/userguide/userguide_single.html#sec:java_library_configurations_graph.
    project.configurations["${variant.name}ApiElements"].outgoing.variants.create("glean-metrics-yaml") { variantConfig ->
        stems.each { stem ->
            def f = file("$projectDir/${stem}.yaml")
            if (f.exists()) {
                variantConfig.artifact(f) { artifact ->
                    artifact.type = "glean-metrics-yaml"
                    // If in the future we want this to be dynamic, we can use
                    // `artifact.builtBy(task)`.
                }
            }
        }
    }
}
